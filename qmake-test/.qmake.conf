# .qmake.conf file acts like a global .pri file that is processed before everything else and is applied to the whole project
isEmpty(QMAKESPEC) : return() # for some reason .qmake.conf is run twice - first before even qmake.conf (located at QMAKESPEC path), and second after qmake.conf
DOTQMAKECONF += $$_FILE_ # meant to be in all .qmake.conf files so it serves as a way to check which .qmake.conf is used

GLOBAL_TEST_VAR += "bazinga"

# arg is full path to .pri # call example: !assureUniquePri($$_FILE_) : return()
defineTest(assureUniquePri) {
    this_pri_file = $$basename(1)
    contains(included_pri_files, $$this_pri_file) : return(false)
    included_pri_files += $$this_pri_file
    export(included_pri_files)
    return(true)
}